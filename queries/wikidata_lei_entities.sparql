SELECT DISTINCT ?entity ?entityLabel ?isin_value ?lei_value ?country ?countryLabel ?country_alpha_2 ?twitter_value WHERE {
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,de,fr,it". }
  {
    SELECT DISTINCT ?entity ?lei_value WHERE {
      {
        {
        # Entities which are instances of businesses or its subclasses
        ?entity p:P31 ?statement0 . 
        ?statement0 (ps:P31/(wdt:P279*)) wd:Q4830453 .
        }
        {
        # Entities with legal entity identifier (LEI)
        ?entity p:P1278 ?legal_entity_prop . 
        ?legal_entity_prop (ps:P1278) ?lei_value .
        }
      }
    }
  }
  OPTIONAL {
      ?entity p:P17 ?statement2 .
      ?statement2 ps:P17 ?country . # Country
      ?country p:P297 ?iso_two_letter .
      ?iso_two_letter ps:P297 ?country_alpha_2 .
  }
  OPTIONAL {
      ?entity (p:P946) ?isin_prop .
      ?isin_prop (ps:P946) ?isin_value . # isin id
  }
  OPTIONAL {
      ?entity (p:P2002) ?twitter_prop .
      ?twitter_prop (ps:P2002) ?twitter_value . # twitter username
  }
}
